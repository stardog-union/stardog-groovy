apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
//apply plugin: 'codenarc'

group = 'com.complexible.stardog'
version = '2.2.2'

repositories {	
    mavenLocal()
    mavenCentral()
}

dependencies {

	// This is still necessary until we separate the embedded server functionality

	compile ('com.complexible.stardog.protocols.snarl:client:2.2.2')
	compile ('com.complexible.stardog:common-rdf:2.2.2')
	compile ('com.complexible.stardog:openrdf-utils:2.2.2')
	compile ('com.complexible.stardog:shared:2.2.2')
	compile ('org.openrdf.sesame:sesame:2.7.12')
	compile ('com.complexible.stardog:api:2.2.2')


	compile ('org.slf4j:slf4j-api:1.6.1')

	compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.2.2'
	testCompile group: 'junit', name: 'junit', version: '4.+'
}

task sourceJar(type: Jar) {
	description 'An archive of the source code for Maven Central'
	classifier 'sources'
	from sourceSets.main.groovy
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from 'build/docs/javadoc'
}


artifacts {
	archives javadocJar, sourceJar
}

signing {
	sign configurations.archives
	required { gradle.taskGraph.hasTask(uploadArchives) }
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signPom(deployment) }

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: sonatypeUsername, password: sonatypePassword)
			}

			pom.project {
				name 'stardog-groovy'
				packaging 'jar'
				description 'Stardog Groovy bindings for building Stardog apps'
				url 'https://github.com/clarkparsia/stardog-groovy'

				scm {
					url 'scm:git://github.com/clarkparsia/stardog-groovy.git'
					connection 'scm:git://github.com/clarkparsia/stardog-groovyl.git'
					developerConnection 'scm:git://github.com/clarkparsia/stardog-groovy.git'
				}

				licenses {
					license {
						name 'The Apache Software License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						distribution 'repo'
					}
				}

				developers {
					developer {
						id 'albakercp'
						name 'Al Baker'
					}
				}
			}
		}
	}
}

configure(install.repositories.mavenInstaller) {
	pom.project {
		version '2.2.1'
		artifactId 'stardog-groovy'
		groupId 'com.complexible.stardog'
	}
}



